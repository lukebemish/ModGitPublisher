plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.18.0'
    id 'org.cadixdev.licenser' version '0.6.1'
}

group 'dev.lukebemish.modgitpublisher'
version '1.0.0'

gradlePlugin {
    plugins {
        modgitpublisher {
            id = 'dev.lukebemish.modgitpublisher'
            implementationClass = 'dev.lukebemish.modgitpublisher.ModGitPublisherPlugin'
        }
    }
}

license {
    header = project.file('header.txt')
}

pluginBundle {
    website = 'https://github.com/lukebemish/ModGitPublisher'
    vcsUrl = 'https://github.com/lukebemish/ModGitPublisher'
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'
    api 'org.eclipse.jgit:org.eclipse.jgit:[6.0,7.0)'
}

tasks.create('publishPluginToPersonalMaven') {
    it.group = 'publishing'
    it.dependsOn(
            'publishPluginMavenPublicationToPersonalMavenRepository',
            'publishModgitpublisherPluginMarkerMavenPublicationToPersonalMavenRepository'
    )
}

publishing {
    repositories {
        maven {
            name = 'PersonalMaven'
            url = uri('https://maven.lukebemish.dev/snapshots')
            credentials {
                username = findProperty('lukebemishMavenUser') ?: ''
                password = findProperty('lukebemishMavenPassword') ?: ''
            }
        }
    }
}